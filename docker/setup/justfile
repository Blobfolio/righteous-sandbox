##
# Docker Builder
##

# Container Version.
sandbox_version := "1.7.0"

# Third-Party Packages.
composer :=  "https://getcomposer.org/download/2.8.6/composer.phar"
oxlint :=    "https://github.com/oxc-project/oxc/releases/download/oxlint_v0.15.11/oxlint-linux-x64-gnu"
phpab :=     "https://github.com/theseer/Autoload/releases/download/1.29.3/phpab-1.29.3.phar"
phpcs :=     "https://apt.blobfolio.com/other/blobfolio-phpcs.tar.gz"
phpunit :=   "https://phar.phpunit.de/phpunit-11.phar"
watchexec := "https://github.com/watchexec/watchexec/releases/download/v2.2.1/watchexec-2.2.1-x86_64-unknown-linux-gnu.deb"



# Install Everything!
@install: install_deb install_manual install_npm clean
	mkdir /share
	echo "{{ sandbox_version }}" > /opt/righteous-sandbox.version
	echo "$( date -u +"%F %T" ) UTC" > /opt/righteous-sandbox.built

	mkdir -p /usr/local/rustup
	mkdir -p /usr/local/cargo

	fyi success "Everything should be set!"


# Install Basic Dependencies.
@install_deb:
	DEBIAN_FRONTEND=noninteractive apt-fast install -y --no-install-recommends \
		autoconf \
		automake \
		b3sum \
		bc \
		brotli \
		build-essential \
		bzip2 \
		cargo-bashman \
		channelz \
		clang-19 lld-19 llvm-19 llvm-19-linker-tools bolt-19 \
		cmake \
		devscripts \
		esbuild \
		file \
		flac \
		flaca \
		flex \
		fyi \
		g++ \
		gcc \
		git \
		guff \
		htminl \
		imagemagick \
		inkscape \
		jq \
		less \
		libaom-dev \
		libbz2-dev \
		libcdio-dev \
		libssl-dev \
		libzip-dev \
		make \
		man \
		mariadb-server \
		meson \
		nasm \
		ninja-build \
		nodejs \
		openssh-client \
		p7zip-full \
		parallel \
		patch \
		php-getid3 \
		php-pear \
		php-php-gettext \
		php-phpseclib \
		php8.4-bcmath \
		php8.4-bz2 \
		php8.4-cli \
		php8.4-common \
		php8.4-curl \
		php8.4-gd \
		php8.4-gmp \
		php8.4-imap \
		php8.4-intl \
		php8.4-mbstring \
		php8.4-mysql \
		php8.4-opcache \
		php8.4-readline \
		php8.4-xml \
		php8.4-yaml \
		php8.4-zip \
		pkg-config \
		procps \
		rsync \
		ruby \
		sass \
		sassc \
		sd \
		serse \
		sqlite3 \
		sudo \
		typos \
		unzip \
		valgrind \
		vim \
		webp \
		whiptail \
		wp-cli \
		xxd \
		xz-utils \
		yasm \
		yesvgmap \
		zip \
		zstd


# Install Third-Party Packages.
@install_manual:
	wget -q -O /tmp/watchexec.deb "{{ watchexec }}"
	dpkg -i /tmp/watchexec.deb
	rm /tmp/watchexec.deb

	wget -q -O /usr/bin/composer "{{ composer }}"
	chmod 755 /usr/bin/composer

	wget -q -O /usr/bin/oxlint "{{ oxlint }}"
	chmod 755 /usr/bin/oxlint

	wget -q -O /usr/bin/phpab "{{ phpab }}"
	chmod 755 /usr/bin/phpab

	wget -q -O /usr/bin/phpunit "{{ phpunit }}"
	chmod 755 /usr/bin/phpunit

	mkdir /opt/phpcs
	wget -q -O /tmp/phpcs.tar.gz "{{ phpcs }}"
	tar -C /opt/phpcs -xvf /tmp/phpcs.tar.gz
	ln -s /opt/phpcs/lib/vendor/bin/phpcs /usr/bin/phpcs
	ln -s /opt/phpcs/lib/vendor/bin/phpcbf /usr/bin/phpcbf


# Install NPM Packages.
@install_npm:
	npm set cache /tmp/npm-cache
	npm set package-lock false
	npm set save false
	#npm set unsafe-perm true
	npm update -g npm
	npm install -g svgo terser


# Cleanup.
@clean:
	DEBIAN_FRONTEND=noninteractive apt-get autoremove --purge -y

	apt-get clean -y
	composer clear
	rm -rf \
		/root/.composer \
		/root/.config \
		/tmp/* \
		/var/lib/apt/lists/* \
		/var/log/*
