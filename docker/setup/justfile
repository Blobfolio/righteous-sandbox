##
# Docker Builder
##

# Container Version.
sandbox_version := "1.6.4"

# Third-Party Packages.
b3sum :=     "https://github.com/BLAKE3-team/BLAKE3/releases/download/1.5.1/b3sum_linux_x64_bin"
composer :=  "https://getcomposer.org/download/2.7.4/composer.phar"
hyperfine := "https://github.com/sharkdp/hyperfine/releases/download/v1.18.0/hyperfine_1.18.0_amd64.deb"
oxlint :=    "https://github.com/oxc-project/oxc/releases/download/oxlint_v0.3.1/oxlint-linux-x64-gnu"
phpab :=     "https://github.com/theseer/Autoload/releases/download/1.29.0/phpab-1.29.0.phar"
phpcs :=     "https://apt.blobfolio.com/other/blobfolio-phpcs.tar.gz"
phpunit :=   "https://phar.phpunit.de/phpunit-11.phar"
watchexec := "https://github.com/watchexec/watchexec/releases/download/v2.1.1/watchexec-2.1.1-x86_64-unknown-linux-gnu.deb"



# Install Everything!
@install: install_deb install_manual install_npm clean
	mkdir /share
	echo "{{ sandbox_version }}" > /opt/righteous-sandbox.version
	echo "$( date -u +"%F %T" ) UTC" > /opt/righteous-sandbox.built

	mkdir -p /usr/local/rustup
	mkdir -p /usr/local/cargo

	fyi success "Everything should be set!"


# Install Basic Dependencies.
@install_deb:
	DEBIAN_FRONTEND=noninteractive apt-fast install -y --no-install-recommends \
		autoconf \
		automake \
		bc \
		brotli \
		build-essential \
		bzip2 \
		cargo-bashman \
		channelz \
		checksame \
		clang-17 lld-17 llvm-17 llvm-17-linker-tools bolt-17 \
		cmake \
		devscripts \
		esbuild \
		file \
		flac1.4 \
		flaca \
		flex \
		fyi \
		g++ \
		gcc \
		git \
		guff \
		htminl \
		imagemagick \
		inkscape \
		jq \
		less \
		libaom-dev \
		libbz2-dev \
		libcdio-dev \
		librust-gdk-dev \
		librust-gtk-dev \
		libssl-dev \
		libzip-dev \
		make \
		man \
		mariadb-server \
		meson \
		nasm \
		ninja-build \
		nodejs \
		openssh-client \
		p7zip-full \
		parallel \
		patch \
		php-getid3 \
		php-pear \
		php-php-gettext \
		php-phpseclib \
		php8.2-bcmath \
		php8.2-bz2 \
		php8.2-cli \
		php8.2-common \
		php8.2-curl \
		php8.2-gd \
		php8.2-gmp \
		php8.2-imap \
		php8.2-intl \
		php8.2-mbstring \
		php8.2-mysql \
		php8.2-opcache \
		php8.2-readline \
		php8.2-xml \
		php8.2-yaml \
		php8.2-zip \
		pkg-config \
		procps \
		rsync \
		ruby \
		sass \
		sassc \
		sd \
		serse \
		sqlite3 \
		sudo \
		typos \
		unzip \
		valgrind \
		vim \
		webp \
		whiptail \
		wp-cli \
		xz-utils \
		yasm \
		yesvgmap \
		zip


# Install Third-Party Packages.
@install_manual:
	wget -q -O /tmp/hyperfine.deb "{{ hyperfine }}"
	dpkg -i /tmp/hyperfine.deb
	rm /tmp/hyperfine.deb

	wget -q -O /tmp/watchexec.deb "{{ watchexec }}"
	dpkg -i /tmp/watchexec.deb
	rm /tmp/watchexec.deb

	wget -q -O /usr/bin/b3sum "{{ b3sum }}"
	chmod 755 /usr/bin/b3sum

	wget -q -O /usr/bin/composer "{{ composer }}"
	chmod 755 /usr/bin/composer

	wget -q -O /usr/bin/oxlint "{{ oxlint }}"
	chmod 755 /usr/bin/oxlint

	wget -q -O /usr/bin/phpab "{{ phpab }}"
	chmod 755 /usr/bin/phpab

	wget -q -O /usr/bin/phpunit "{{ phpunit }}"
	chmod 755 /usr/bin/phpunit

	mkdir /opt/phpcs
	wget -q -O /tmp/phpcs.tar.gz "{{ phpcs }}"
	tar -C /opt/phpcs -xvf /tmp/phpcs.tar.gz
	ln -s /opt/phpcs/lib/vendor/bin/phpcs /usr/bin/phpcs
	ln -s /opt/phpcs/lib/vendor/bin/phpcbf /usr/bin/phpcbf


# Install NPM Packages.
@install_npm:
	npm set cache /tmp/npm-cache
	npm set package-lock false
	npm set save false
	#npm set unsafe-perm true
	npm update -g npm
	npm install -g svgo terser


# Cleanup.
@clean:
	DEBIAN_FRONTEND=noninteractive apt-get autoremove --purge -y

	apt-get clean -y
	composer clear
	rm -rf \
		/root/.composer \
		/root/.config \
		/tmp/* \
		/var/lib/apt/lists/* \
		/var/log/*
